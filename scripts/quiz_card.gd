class_name QuizCard
## シェア用の結果カード（1080x1350 の PNG）。SubViewport に 2D のカードと 3D のマイおばけ猫を組んで撮る。
## 使い方: var img: Image = await QuizCard.render(self, result)
##        QuizCard.deliver(img, type_id) … web ならダウンロード、デスクトップなら user:// に保存（戻り値は案内文）

const W := 1080
const H := 1350
const INK := Color("2a2233")
const SUB := Color("6a5f70")
const CREAM := Color("fbf3ea")


static func render(host: Node, result: Dictionary) -> Image:
	var type_id: String = result.type_id
	var t: Dictionary = QuizData.TYPES[type_id]
	var col := QuizData.tone(type_id)
	var black: FontFile = load("res://assets/fonts/ZenMaruGothic-Black.ttf")
	var bold: FontFile = load("res://assets/fonts/ZenMaruGothic-Bold.ttf")

	var vp := SubViewport.new()
	vp.size = Vector2i(W, H)
	vp.transparent_bg = false
	vp.render_target_update_mode = SubViewport.UPDATE_ALWAYS
	vp.canvas_item_default_texture_filter = Viewport.DEFAULT_CANVAS_ITEM_TEXTURE_FILTER_LINEAR
	host.add_child(vp)

	var bg := ColorRect.new()
	bg.color = CREAM
	bg.size = Vector2(W, H)
	vp.add_child(bg)

	# ロゴ
	var logo := _label("Paw Time", black, 64, INK)
	logo.position = Vector2(0, 52)
	logo.size = Vector2(W, 80)
	logo.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	vp.add_child(logo)
	var kicker := _label(UI.t("マイおばけ猫 診断"), bold, 32, col.darkened(0.45))
	kicker.position = Vector2(0, 136)
	kicker.size = Vector2(W, 44)
	kicker.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	vp.add_child(kicker)

	# おばけの舞台（色付きの角丸）
	var stage := Panel.new()
	var ss := StyleBoxFlat.new()
	ss.bg_color = col.lightened(0.62)
	ss.set_corner_radius_all(64)
	stage.add_theme_stylebox_override("panel", ss)
	stage.position = Vector2(90, 206)
	stage.size = Vector2(900, 560)
	vp.add_child(stage)
	var box := SubViewportContainer.new()
	box.stretch = true
	box.position = stage.position
	box.size = stage.size
	vp.add_child(box)
	var vp3 := SubViewport.new()
	vp3.own_world_3d = true
	vp3.transparent_bg = true
	vp3.msaa_3d = Viewport.MSAA_4X
	vp3.render_target_update_mode = SubViewport.UPDATE_ALWAYS
	box.add_child(vp3)
	var ob := _stage3d(vp3, t.look)
	# 舞台の中の小さなタグ（向いてる仕事）
	var job := _chip(UI.t("向いてる仕事  ") + QuizData.JOBS[t.job].ja, bold, 30, Color.WHITE, col.darkened(0.35))
	job.position = Vector2(126, 238)
	vp.add_child(job)

	# タイプ名と一言
	var pre := _label(UI.t("わたしのマイおばけ猫は"), bold, 34, SUB)
	pre.position = Vector2(0, 790)
	pre.size = Vector2(W, 48)
	pre.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	vp.add_child(pre)
	var shown_name: String = t.en_name if UI.is_en() else t.name
	var name_l := _label(shown_name, black, (84 if shown_name.length() <= 9 else 72) if not UI.is_en() else (72 if shown_name.length() <= 18 else 58), INK)
	name_l.position = Vector2(0, 836)
	name_l.size = Vector2(W, 112)
	name_l.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	name_l.vertical_alignment = VERTICAL_ALIGNMENT_CENTER
	vp.add_child(name_l)
	var en := _label(t.name if UI.is_en() else t.en_name, bold, 30, col.darkened(0.4))
	en.position = Vector2(0, 946)
	en.size = Vector2(W, 42)
	en.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	vp.add_child(en)
	var line := _label(t.en_line if UI.is_en() else t.line, bold, 42 if not UI.is_en() else 36, INK)
	line.position = Vector2(0, 1000)
	line.size = Vector2(W, 60)
	line.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	vp.add_child(line)

	# 4 つの軸（2 列 × 2 段）
	var axes: Array = axes_of(result)
	for i in 4:
		var bar := _axis_bar(i, axes[i], col, bold, 30, Vector2(420, 18))
		bar.position = Vector2(90 + (i % 2) * 480, 1086 + (i / 2) * 78)
		vp.add_child(bar)

	# フッター
	var url := _label(QuizData.SITE_URL.replace("https://", ""), bold, 28, SUB)
	url.position = Vector2(0, 1262)
	url.size = Vector2(W, 40)
	url.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	vp.add_child(url)

	# 3D と文字が描かれるまで数フレーム待つ
	for i in 4:
		await RenderingServer.frame_post_draw
	var img := vp.get_texture().get_image()
	ob.queue_free()
	vp.queue_free()
	return img


## 3D の舞台（光・カメラ・おばけ・足元の影）を組む
static func _stage3d(vp3: SubViewport, look: Dictionary) -> MyObake3D:
	var world := Node3D.new()
	vp3.add_child(world)
	var env := Environment.new()
	env.background_mode = Environment.BG_CLEAR_COLOR
	env.ambient_light_source = Environment.AMBIENT_SOURCE_COLOR
	env.ambient_light_color = Color("fff2e6")
	env.ambient_light_energy = 0.45
	env.tonemap_mode = Environment.TONE_MAPPER_LINEAR
	var we := WorldEnvironment.new()
	we.environment = env
	world.add_child(we)
	var sun := DirectionalLight3D.new()
	sun.rotation_degrees = Vector3(-35, 30, 0)
	sun.light_color = Color("fff0dc")
	sun.light_energy = 0.8
	world.add_child(sun)
	var cam := Camera3D.new()
	cam.fov = 27
	cam.position = Vector3(0, 0.95, 3.4)
	world.add_child(cam)
	cam.look_at(Vector3(0, 0.6, 0))
	var shadow := MeshInstance3D.new()
	var sm := CylinderMesh.new()
	sm.top_radius = 0.5
	sm.bottom_radius = 0.5
	sm.height = 0.005
	shadow.mesh = sm
	var smat := StandardMaterial3D.new()
	smat.shading_mode = BaseMaterial3D.SHADING_MODE_UNSHADED
	smat.transparency = BaseMaterial3D.TRANSPARENCY_ALPHA
	smat.albedo_color = Color(0.16, 0.13, 0.2, 0.14)
	shadow.material_override = smat
	shadow.scale = Vector3(1.1, 1, 0.5)
	world.add_child(shadow)
	var ob := MyObake3D.new().setup_look(look)
	ob.rotation.y = 0.35
	world.add_child(ob)
	ob.hold_still()
	return ob


static func _label(text: String, font: FontFile, size: int, color: Color) -> Label:
	var l := Label.new()
	l.text = text
	l.add_theme_font_override("font", font)
	l.add_theme_font_size_override("font_size", size)
	l.add_theme_color_override("font_color", color)
	return l


static func _chip(text: String, font: FontFile, size: int, bg: Color, fg: Color) -> PanelContainer:
	var p := PanelContainer.new()
	var s := StyleBoxFlat.new()
	s.bg_color = bg
	s.set_corner_radius_all(40)
	s.content_margin_left = 26
	s.content_margin_right = 26
	s.content_margin_top = 8
	s.content_margin_bottom = 10
	p.add_theme_stylebox_override("panel", s)
	p.add_child(_label(text, font, size, fg))
	return p


## 軸の棒。上に「外へ ◯◯% / 内へ」、下に 2 色の棒。reveal 画面でも使うので寸法を渡せるようにする。
## 軸の割合。答えが残っていれば、答えから出し直す（古い保存は縮めた値のため）
static func axes_of(result: Dictionary) -> Array:
	var ans: String = result.get("answers", "")
	if ans.length() >= QuizData.QUESTIONS.size():
		return QuizData.score(ans).axes
	return result.get("axes", [0.5, 0.5, 0.5, 0.5])


static func _axis_bar(axis: int, ratio: float, col: Color, font: FontFile, size: int, bar_size: Vector2) -> VBoxContainer:
	var ax: Dictionary = QuizData.AXES[axis]
	var v := VBoxContainer.new()
	v.add_theme_constant_override("separation", int(size * 0.2))
	var row := HBoxContainer.new()
	var lean_a := ratio >= 0.5
	var la := _label(ax.a_en if UI.is_en() else ax.a, font, size, INK if lean_a else SUB.lightened(0.3))
	var lb := _label(ax.b_en if UI.is_en() else ax.b, font, size, INK if not lean_a else SUB.lightened(0.3))
	var pct := _label("%d%%" % roundi((ratio if lean_a else 1.0 - ratio) * 100), font, size, col.darkened(0.35))
	pct.size_flags_horizontal = Control.SIZE_EXPAND_FILL
	pct.horizontal_alignment = HORIZONTAL_ALIGNMENT_CENTER
	row.add_child(la)
	row.add_child(pct)
	row.add_child(lb)
	row.custom_minimum_size.x = bar_size.x
	v.add_child(row)
	var track := Panel.new()
	var ts := StyleBoxFlat.new()
	ts.bg_color = Color(0.16, 0.13, 0.2, 0.1)
	ts.set_corner_radius_all(int(bar_size.y / 2))
	track.add_theme_stylebox_override("panel", ts)
	track.custom_minimum_size = bar_size
	v.add_child(track)
	var fill := Panel.new()
	var fs := StyleBoxFlat.new()
	fs.bg_color = col.darkened(0.15)
	fs.set_corner_radius_all(int(bar_size.y / 2))
	fill.add_theme_stylebox_override("panel", fs)
	# 前の極は左から、後ろの極は右から伸ばす
	var w := bar_size.x * maxf(0.08, ratio if lean_a else 1.0 - ratio) # 0% でも少しは見える
	fill.size = Vector2(w, bar_size.y)
	fill.position = Vector2(0.0 if lean_a else bar_size.x - w, 0)
	track.add_child(fill)
	return v


## 画像を手元に届ける。web はダウンロード、それ以外は user:// に保存。戻り値は画面に出す案内文。
static func deliver(img: Image, type_id: String) -> String:
	var file := "paw_time_my_obake_%s.png" % type_id
	if OS.has_feature("web"):
		JavaScriptBridge.download_buffer(img.save_png_to_buffer(), file, "image/png")
		return UI.t("画像をダウンロードしました")
	var path := "user://" + file
	var err := img.save_png(path)
	if err != OK:
		return UI.t("保存できませんでした（%s）") % error_string(err)
	return UI.t("保存しました：") + ProjectSettings.globalize_path(path)
